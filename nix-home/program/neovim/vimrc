""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Vincent Desjardins (vdesjardins@gmail.com)
" Date: 2010/10/01 21:48:28
"
set nocompatible

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => General functions
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! MySys()
  if has("win32") || has("win64")
    return "windows"
  elseif has("unix")
    return "unix"
  endif
endfunction

" force eencoding to utf-8 on windows
if MySys() ==? "windows"
  if has("multi_byte")
    if &termencoding ==? ""
      let &termencoding = &encoding
    endif
    set encoding=utf-8
    setglobal fileencoding=utf-8
    "setglobal bomb
    set fileencodings=ucs-bom,utf-8,latin1
  endif

  set langmenu=en_US.UTF-8  " sets the language of the menu (gvim)
  language English          " sets the language of the messages / ui (vim)

  au GUIEnter * simalt ~n
endif

filetype off

" vim-plug setup --------------------------- {{{

if MySys() ==? "windows"
  if has('nvim')
    let s:autoloaddir = expand('~\AppData\Local\nvim\autoload')
  else
    let s:autoloaddir = expand('~\vimfiles\autoload')
  endif

  if empty(glob(expand(s:autoloaddir . '\plug.vim')))
    silent execute "!powershell -NoProfile -Command " .
         \ "New-Item -ItemType Directory -Force -Path " . s:autoloaddir
    silent execute "!powershell -NoExit -NoProfile -Command " .
         \ "(New-Object Net.WebClient).DownloadFile('https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim', " .
         \ "'" . s:autoloaddir . "\\plug.vim')"
    autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
  endif
  let s:vimplugdir = expand('~\vimfiles\plugged')
else
  if has('nvim')
    let s:autoloaddir = expand('~/.local/share/nvim/site/autoload')
  else
    let s:autoloaddir = expand('~/.vim/autoload')
  endif

  if empty(glob(s:autoloaddir . '/plug.vim'))
    silent execute "!curl -fLo " . s:autoloaddir . "/plug.vim --create-dirs " .
         \ "https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim"
    autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
  endif

  let s:vimplugdir = expand('~/.vim/plugged')
endif

call plug#begin(s:vimplugdir)

Plug 'houtsnip/vim-emacscommandline'
Plug 'ekalinin/Dockerfile.vim'
Plug 'edkolev/tmuxline.vim'
Plug 'edkolev/promptline.vim'
Plug 'mogelbrod/vim-jsonpath'
Plug 'towolf/vim-helm'

Plug 'chriskempson/base16-vim'

call plug#end()

" }}}

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" General
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set colorcolumn=85

set undofile
set undolevels=1000

set nobackup
set nowritebackup

" Permit extra key combinations
let mapleader = ","
let g:mapleader = ","
let maplocalleader = "\<Space>"

" Fast saving
nnoremap <leader>w :w!<cr>

" Save file with sudo
nnoremap <leader>W :w !sudo tee % > /dev/null<cr>

" Fast editing of the .vimrc
if MySys() ==? "unix"
  noremap <leader>e :e! ~/.vimrc<cr>
else
  noremap <leader>e :e! ~/_vimrc<cr>
endif

" Fast editing of tmux.conf
noremap <leader>x :e! ~/.tmux.conf<cr>

" wHen vimrc is edited, reload it
augroup _vimrc_reload_group
autocmd!
autocmd bufwritepost .vimrc source ~/.vimrc
augroup END

" Switch quicky for pasting text in a SSH session.
set pastetoggle=<F2>

" Disable arrow keys
nnoremap <up> <nop>
nnoremap <down> <nop>
nnoremap <left> <nop>
nnoremap <right> <nop>
inoremap <up> <nop>
inoremap <down> <nop>
inoremap <left> <nop>
inoremap <right> <nop>

nnoremap <leader>ws :StripWhitespace<cr>

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" User interface
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Set 7 lines to the curors - when moving vertical..
set so=7

set cmdheight=2 "The commandbar height

set nobackup
set nowritebackup
set updatetime=300
set shortmess+=c
" Always show the signcolumn, otherwise it would shift the text each time
" diagnostics appear/become resolved.
if has("patch-8.1.1564")
  " Recently vim can merge signcolumn and number column into one
  set signcolumn=number
else
  set signcolumn=yes
endif

set hid "Change buffer - without saving

set whichwrap+=<,>,h,l

set ignorecase "Ignore case when searching

set hlsearch "Highlight search things

set magic "Set magic on, for regular expressions

set showmatch "Show matching bracets when text indicator is over them
set mat=2 "How many tenths of a second to blink

" No sound on errors
set noerrorbells
set novisualbell
set t_vb=

set nolist
set listchars=tab:▸\ ,eol:¬

highlight TabLineSel ctermbg=2
highlight ExtraWhitespace ctermbg=167 guibg=#fb4934

" Remove toolbar
set guioptions-=T

" Disable ALT keys for menu
set winaltkeys=no

" format everything
autocmd BufWritePre * :Neoformat

" Vimscript file settings ---------------------- {{{
augroup vim_group
  autocmd!
  autocmd FileType vim set ai sw=2 sts=2 et
  autocmd FileType vim setlocal foldmethod=marker
augroup END
" }}}

" Text, tab and indent related ---------------------- {{{
augroup myfiletypes_group
  " Clear old autocmds in group
  autocmd!
  autocmd FileType ruby,eruby,yaml,ru set ai sw=2 sts=2 et
  autocmd FileType lua set ai sw=2 sts=2 et
  autocmd FileType python set ai sw=4 sts=4 et ts=8
  autocmd FileType sh set noet ci pi sts=0 sw=8 ts=8
  autocmd FileType javascript,json set ai sw=2 sts=2 et
  autocmd FileType java set ai sw=4 sts=4 et
  autocmd FileType html set ai sw=2 sts=2 et
  autocmd FileType objc set ai sw=4 sts=4 et
  autocmd FileType sql set ai sw=4 sts=4 et
  autocmd FileType puppet set ai sw=2 sts=2 et
  autocmd FileType spec set ai sw=2 sts=2 et
  autocmd FileType properties set ai sw=2 sts=2 et
  autocmd FileType jproperties set ai sw=2 sts=2 et
  autocmd FileType xml set ai sw=4 sts=4 et
augroup END

if has("autocmd")
  au BufReadPost * if line("'\"") > 0 && line("'\"") <= line("$")
    \| exe "normal g'\"" | endif
endif
" }}}

autocmd FileType json syntax match Comment +\/\/.\+$+
" Visual mode related --------------------------- {{{
" Really useful!
"  In visual mode when you press * or # to search for the current selection
vnoremap <silent> * :call VisualSearch('f')<CR>
vnoremap <silent> # :call VisualSearch('b')<CR>

" When you press gv you vimgrep after the selected text
vnoremap <silent> gv :call VisualSearch('gv')<CR>

function! CmdLine(str)
  exe "menu Foo.Bar :" . a:str
  emenu Foo.Bar
  unmenu Foo
endfunction

" From an idea by Michael Naumann
function! VisualSearch(direction) range
  let l:saved_reg = @"
  execute "normal! vgvy"

  let l:pattern = escape(@", '\\/.*$^~[]')
  let l:pattern = substitute(l:pattern, "\n$", "", "")

  if a:direction ==? 'b'
    execute "normal ?" . l:pattern . "^M"
  elseif a:direction ==? 'gv'
    call CmdLine("vimgrep " . '/'. l:pattern . '/' . ' **/*.')
  elseif a:direction ==? 'f'
    execute "normal /" . l:pattern . "^M"
  endif

  let @/ = l:pattern
  let @" = l:saved_reg
endfunction

" Reslect last text edited or pasted
nnoremap gV `[v`]

" Bubble single lines
nnoremap <C-Up> [e
nnoremap <C-Down> ]e
" bubble multiple lines"{{{"}}}
vnoremap <C-Up> [egv
vnoremap <C-Down> ]egv

" }}}

" Command mode related ---------------------------------- {{{
" Smart mappings on the command line
cnoremap $h e ~/
cnoremap $d e ~/Desktop/
cnoremap $j e ./
cnoremap $p e ~/projects/
cnoremap $c e <C-\>eCurrentFileDir("e")<cr>

" $q is super useful when browsing on the command line
cnoremap $q <C-\>eDeleteTillSlash()<cr>

func! Cwd()
  let cwd = getcwd()
  return "e " . cwd
endfunc

func! DeleteTillSlash()
  let g:cmd = getcmdline()
  if MySys() ==? "unix"
    let g:cmd_edited = substitute(g:cmd, "\\(.*\[/\]\\).*", "\\1", "")
  else
    let g:cmd_edited = substitute(g:cmd, "\\(.*\[\\\\]\\).*", "\\1", "")
  endif
  if g:cmd ==? g:cmd_edited
    if MySys() ==? "unix"
      let g:cmd_edited = substitute(g:cmd, "\\(.*\[/\]\\).*/", "\\1", "")
    else
      let g:cmd_edited = substitute(g:cmd, "\\(.*\[\\\\\]\\).*\[\\\\\]", "\\1", "")
    endif
  endif
  return g:cmd_edited
endfunc

func! CurrentFileDir(cmd)
  return a:cmd . " " . expand("%:p:h") . "/"
endfunc
" }}}

" Moving around, tabs and buffers ------------------------------------ {{{

" Clear search highlighting
noremap <silent> <leader><cr> :noh<cr>

" Smart way to move between windows
noremap <C-j> <C-W>j
noremap <C-k> <C-W>k
noremap <C-h> <C-W>h
noremap <C-l> <C-W>l

" List buffers
noremap <leader>l :buffers<cr>

" Close the current buffer
noremap <leader>bc :Bclose<cr>

" Close all the buffers
noremap <leader>ba :1,300 bd!<cr>

" Buffer next/previous
noremap <leader>bn :bn<cr>
noremap <leader>bp :bp<cr>

" Use the arrows to something useful
noremap <right> :bn<cr>
noremap <left> :bp<cr>

" When pressing <leader>ccd switch working directory to the directory of the open buffer
noremap <leader>ccd :cd %:p:h<cr>

command! Bclose call <SID>BufcloseCloseIt()
function! <SID>BufcloseCloseIt()
   let l:currentBufNum = bufnr("%")
   let l:alternateBufNum = bufnr("#")

   if buflisted(l:alternateBufNum)
     buffer #
   else
     bnext
   endif

   if bufnr("%") ==? l:currentBufNum
     new
   endif

   if buflisted(l:currentBufNum)
     execute("bdelete! ".l:currentBufNum)
   endif
endfunction

" Specify the behavior when switching between buffers
try
  set switchbuf=usetab
  set stal=2
catch
endtry

" }}}

" Parenthesis/bracket expanding ----------------------- {{{
vnoremap $1 <esc>`>a)<esc>`<i(<esc>
vnoremap $2 <esc>`>a]<esc>`<i[<esc>
vnoremap $3 <esc>`>a}<esc>`<i{<esc>
vnoremap $$ <esc>`>a"<esc>`<i"<esc>
vnoremap $q <esc>`>a'<esc>`<i'<esc>
vnoremap $e <esc>`>a"<esc>`<i"<esc>
" }}}

" General Abbrevs ------------------------------------ {{{
iabbrev vdes Vincent Desjardins
iabbrev xdate <c-r>=strftime("%Y/%m/%d %H:%M:%S")<cr>
iabbrev @@    vdesjardins@gmail.com
iabbrev ccopy Copyright 2014 Vincent Desjardins, all rights reserved.
iabbrev ssig -- <cr>Vincent Desjardins<cr>vdesjardins@gmail.com
" }}}

" Editing mappings --------------------------------------- {{{
"Move a line of text using ALT+[jk] or Comamnd+[jk] on mac
nnoremap <M-j> mz:m+<cr>`z
nnoremap <M-k> mz:m-2<cr>`z
vnoremap <M-j> :m'>+<cr>`<my`>mzgv`yo`z
vnoremap <M-k> :m'<-2<cr>`>my`<mzgv`yo`z

" }}}

" Cope (quickfix) ---------------------------------- {{{
noremap <leader>co :botright cope<cr>
noremap <leader>cc :cclose<cr>
noremap <leader>n :cnext<cr>
noremap <leader>p :cprevious<cr>
" }}}

" Help ---------------------------------- {{{
noremap <leader>hc :helpclose<cr>
noremap <leader>hg :helpgrep
" }}}

" bufExplorer plugin ----------------------------- {{{
let g:bufExplorerDefaultHelp=0
let g:bufExplorerShowRelativePath=1
" }}}

" Make Green ------------------------------ {{{
noremap <Leader>] <Plug>MakeGreen
" }}}

" Spell checking ------------------------------- {{{
"Pressing ,ss will toggle and untoggle spell checking
noremap <leader>ss :setlocal spell!<cr>

"Shortcuts using <leader>
noremap <leader>sn ]s
noremap <leader>sp [s
noremap <leader>sa zg
noremap <leader>s? z=
" }}}

" Markdown -------------------------------------- {{{
augroup markdown_group
  autocmd!
  autocmd BufRead,BufNewFile *.md setlocal spell
  autocmd BufRead,BufNewFile *.md setlocal textwidth=80
augroup END
" }}}

" gitcommit --------------------------------------- {{{
augroup gitcommit_group
  autocmd!
  autocmd FileType gitcommit setlocal textwidth=72
  autocmd FileType gitcommit setlocal spell
augroup END
" }}}

" coc ------------------------------------------------ {{{
" " Use tab for trigger completion with characters ahead and navigate.
" NOTE: Use command ':verbose imap <tab>' to make sure tab is not mapped by
" other plugin before putting this into your config.
inoremap <silent><expr> <TAB>
      \ pumvisible() ? "\<C-n>" :
      \ <SID>check_back_space() ? "\<TAB>" :
      \ coc#refresh()
inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

function! s:check_back_space() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~# '\s'
endfunction

" Use <c-space> to trigger completion.
if has('nvim')
  inoremap <silent><expr> <c-space> coc#refresh()
else
  inoremap <silent><expr> <c-@> coc#refresh()
endif

" Make <CR> auto-select the first completion item and notify coc.nvim to
" format on enter, <cr> could be remapped by other vim plugin
" With a tweak from https://github.com/tpope/vim-endwise/issues/109
let g:endwise_no_mappings = v:true
let g:AutoPairsMapCh = v:false
inoremap <silent><expr> <Plug>CustomCocCR pumvisible() ? coc#_select_confirm() : "\<C-g>u\<CR>\<c-r>=coc#on_enter()\<CR>"
imap <CR> <Plug>CustomCocCR<Plug>DiscretionaryEnd<Plug>AutoPairsReturn

" Use `[g` and `]g` to navigate diagnostics
nmap <silent> [g <Plug>(coc-diagnostic-prev)
nmap <silent> ]g <Plug>(coc-diagnostic-next)

" GoTo code navigation.
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gy <Plug>(coc-type-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-references)

" Use K to show documentation in preview window.
nnoremap <silent> K :call <SID>show_documentation()<CR>

function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  else
    call CocAction('doHover')
  endif
endfunction

" Highlight the symbol and its references when holding the cursor.
autocmd CursorHold * silent call CocActionAsync('highlight')

" Symbol renaming.
nmap <leader>rn <Plug>(coc-rename)

" Formatting selected code.
xmap <leader>f  <Plug>(coc-format-selected)
nmap <leader>f  <Plug>(coc-format-selected)

augroup coc-group
  autocmd!
  " Setup formatexpr specified filetype(s).
  autocmd FileType typescript,json setl formatexpr=CocAction('formatSelected')
  " Update signature help on jump placeholder.
  autocmd User CocJumpPlaceholder call CocActionAsync('showSignatureHelp')
augroup end

" Applying codeAction to the selected region.
" Example: `<leader>aap` for current paragraph
xmap <leader>a  <Plug>(coc-codeaction-selected)
nmap <leader>a  <Plug>(coc-codeaction-selected)

" Remap keys for applying codeAction to the current line.
nmap <leader>ac  <Plug>(coc-codeaction)
" Apply AutoFix to problem on the current line.
nmap <leader>qf  <Plug>(coc-fix-current)

" Introduce function text object
" NOTE: Requires 'textDocument.documentSymbol' support from the language server.
xmap if <Plug>(coc-funcobj-i)
xmap af <Plug>(coc-funcobj-a)
omap if <Plug>(coc-funcobj-i)
omap af <Plug>(coc-funcobj-a)
xmap ic <Plug>(coc-classobj-i)
omap ic <Plug>(coc-classobj-i)
xmap ac <Plug>(coc-classobj-a)
omap ac <Plug>(coc-classobj-a)

" Remap <C-f> and <C-b> for scroll float windows/popups.
if has('nvim-0.4.0') || has('patch-8.2.0750')
  nnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
  nnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
  inoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(1)\<cr>" : "\<Right>"
  inoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(0)\<cr>" : "\<Left>"
  vnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
  vnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
endif

" Use CTRL-S for selections ranges.
" Requires 'textDocument/selectionRange' support from the language server.
nmap <silent> <C-s> <Plug>(coc-range-select)
xmap <silent> <C-s> <Plug>(coc-range-select)

" Add `:Format` command to format current buffer.
command! -nargs=0 Format :call CocAction('format')

" Add `:Fold` command to fold current buffer.
command! -nargs=? Fold :call CocAction('fold', <f-args>)

" Add `:OR` command for organize imports of the current buffer.
command! -nargs=0 OR   :call CocAction('runCommand', 'editor.action.organizeImport')

" Add (Neo)Vim's native statusline support.
" NOTE: Please see `:h coc-status` for integrations with external plugins that
" provide custom statusline: lightline.vim, vim-airline.
" set statusline^=%{coc#status()}%{get(b:,'coc_current_function','')}

" Mappings using CocFzfCList:
nnoremap <silent><nowait> <space><space>  :<C-u>CocFzfList<cr>
" Show all diagnostics.
nnoremap <silent><nowait> <space>a  :<C-u>CocFzfList diagnostics<cr>
" Show all diagnostics current buffer.
nnoremap <silent><nowait> <space>b  :<C-u>CocFzfList diagnostics --current-buf<cr>
" Manage extensions.
nnoremap <silent><nowait> <space>e  :<C-u>CocFzfList extensions<cr>
" Show commands.
nnoremap <silent><nowait> <space>c  :<C-u>CocFzfList commands<cr>
" jump locations
nnoremap <silent><nowait> <space>l  :<C-u>CocFzfList location<cr>
" Find symbol of current document.
nnoremap <silent><nowait> <space>o  :<C-u>CocFzfList outline<cr>
" Search workspace symbols.
nnoremap <silent><nowait> <space>s  :<C-u>CocFzfList symbols<cr>
" Search Files
nnoremap <silent><nowait> <space>f  :<C-u>CocFzfList files<cr>
" Search Text
nnoremap <silent><nowait> <space>g  :<C-u>CocFzfList grep<cr>
" Do default action for next item.
nnoremap <silent><nowait> <space>j  :<C-u>CocNext<CR>
" Do default action for previous item.
nnoremap <silent><nowait> <space>k  :<C-u>CocPrev<CR>
" Resume latest coc list.
nnoremap <silent><nowait> <space>p  :<C-u>CocFzfListResume<CR>

" }}}

" coc-fzf ------------------------------------------ {{{
let g:fzf_layout = { 'window': { 'width': 0.9, 'height': 0.6 } }
let g:coc_fzf_preview = ''
let g:coc_fzf_opts = []
" }}}
"
" coc-snippets ------------------------------------------ {{{
" Use <C-l> for trigger snippet expand.
imap <C-l> <Plug>(coc-snippets-expand)

" Use <C-j> for select text for visual placeholder of snippet.
vmap <C-j> <Plug>(coc-snippets-select)

" Use <C-j> for jump to next placeholder, it's default of coc.nvim
let g:coc_snippet_next = '<c-j>'

" Use <C-k> for jump to previous placeholder, it's default of coc.nvim
let g:coc_snippet_prev = '<c-k>'

" Use <C-j> for both expand and jump (make expand higher priority.)
imap <C-j> <Plug>(coc-snippets-expand-jump)

" Use <leader>x for convert visual selected code to snippet
xmap <leader>x  <Plug>(coc-convert-snippet)

" Using tab too
inoremap <silent><expr> <TAB>
      \ pumvisible() ? coc#_select_confirm() :
      \ coc#expandableOrJumpable() ? "\<C-r>=coc#rpc#request('doKeymap', ['snippets-expand-jump',''])\<CR>" :
      \ <SID>check_back_space() ? "\<TAB>" :
      \ coc#refresh()

function! s:check_back_space() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~# '\s'
endfunction

let g:coc_snippet_next = '<tab>'
" }}}

" Vim grep ------------------------------------------ {{{
let Grep_Skip_Dirs = 'RCS CVS SCCS .svn generated .git log logs tmp'
set grepprg=rg\ --vimgrep
" }}}

" coc-yank ------------------------------------------ {{{
noremap <silent> <space>y  :<C-u>CocFzfList yank<cr>
" }}}

" MISC ---------------------------------------- {{{
" Remove the Windows ^M - when the encodings gets messed up
noremap <Leader>wm mmHmt:%s/<C-V><cr>//ge<cr>'tzt'm

"Quickly open a buffer for scripbble
noremap <leader>q :e ~/buffer<cr>

" share clipboard with windows clipboard
if MySys() ==? "windows"
  set clipboard+=unnamed
endif

set hidden

" }}}

" Copy filename to clipboard --------------------- {{{
" Convert slashes to backslashes for Windows.
if MySys() ==? "windows"
  nnoremap <Leader>cs :let @*=substitute(expand("%"), "/", "\\", "g")<CR>
  nnoremap <Leader>cl :let @*=substitute(expand("%:p"), "/", "\\", "g")<CR>

  " This will copy the path in 8.3 short format, for DOS and Windows 9x
  nnoremap <Leader>c8 :let @*=substitute(expand("%:p:8"), "/", "\\", "g")<CR>
else
  nnoremap <Leader>cs :let @*=expand("%")<CR>
  nnoremap <Leader>cl :let @*=expand("%:p")<CR>
  " Add it to the '+' register too for gnome clipboard.
  nnoremap <Leader>cs :let @+=expand("%")<CR>
  nnoremap <Leader>cl :let @+=expand("%:p")<CR>
endif

" zen-coding
let g:user_zen_settings = {
      \   'indentation': "  ",
      \   'php': {
      \      'extends': 'html',
      \   }
      \}

" For MRU plugin
nnoremap <Leader>r :MRU<space>

" Disable Lusty on windows for now
if MySys() ==? "windows"
  let g:LustyExplorerSuppressRubyWarning = 1
endif

" }}}

" FZF --------------------------------- {{{
let g:fzf_buffers_jump = 1
let g:fzf_command_prefix = 'FZF'
noremap <leader>t :FZF<cr>
noremap <leader>b :FZFBuffers<cr>
noremap <leader>f :FZFTags<cr>
noremap <leader>m :FZFMarks<cr>


command! -bang -nargs=* Rg
      \ call fzf#vim#grep(
      \   'rg --column --line-number --color=always --ignore-case '.shellescape(<q-args>), 1,
      \   <bang>0 ? fzf#vim#with_preview('up:60%')
      \           : fzf#vim#with_preview('right:50%:hidden', '?'),
      \   <bang>0)

noremap <leader>g :Rg<cr>

" }}}

" rainbow parentheses ------------------------- {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
augroup rainbow_group
  autocmd!
  autocmd VimEnter * RainbowParenthesesToggle
  autocmd Syntax * RainbowParenthesesLoadRound
  autocmd Syntax * RainbowParenthesesLoadSquare
  autocmd Syntax * RainbowParenthesesLoadBraces
augroup END
" }}}

" UltiSnips parentheses ----------------------- {{{
let g:UltiSnipsExpandTrigger = '<c-l>'
let g:UltiSnipsJumpForwardTrigger = '<c-j>'
let g:UltiSnipsJumpBackwardTrigger = '<c-k>'
" }}}

" tagbar ------------------------------- {{{
nnoremap <F8> :TagbarToggle<CR>
" }}}

" airline -------------------------------- {{{
let g:airline_powerline_fonts = 1
" }}}

" promptline -------------------------------- {{{
let g:promptline_preset = {
        \'a' : [ promptline#slices#host() ],
        \'b' : [ '$USER'],
        \'c' : [ promptline#slices#cwd() ],
        \'y' : [ promptline#slices#vcs_branch(), promptline#slices#jobs() ],
        \'warn' : [ promptline#slices#last_exit_code() ]}
" }}}

" ale -------------------------------- {{{
function! LinterStatus() abort
    let l:counts = ale#statusline#Count(bufnr(''))

    let l:all_errors = l:counts.error + l:counts.style_error
    let l:all_non_errors = l:counts.total - l:all_errors

    return l:counts.total == 0 ? 'OK' : printf(
    \   '%dW %dE',
    \   all_non_errors,
    \   all_errors
    \)
endfunction

set statusline+=%#warningmsg#
set statusline=%{LinterStatus()}
set statusline+=%*

let g:ale_virtualtext_cursor=1

" }}}

" ZoomWin -------------------------------- {{{
noremap <leader>z :ZoomWin<CR>
" }}}

" vimrooter -------------------------------- {{{
let g:rooter_patterns = ['.root', '.git/']
" }}}

" vim-easy-align -------------------------------- {{{
" Start interactive EasyAlign in visual mode (e.g. vipga)
xmap ga <Plug>(EasyAlign)
" Start interactive EasyAlign for a motion/text object (e.g. gaip)
nmap ga <Plug>(EasyAlign)
" }}}

" Task List --------------------------------------------- {{{
map <leader>v <Plug>TaskList
" }}}
"
" easytags --------------------------------------------- {{{
set tags=./tags;
let g:easytags_dynamic_files = 1
let g:easytags_suppress_ctags_warning = 1
" }}}
"
" snippet --------------------------------------------- {{{
set tags=./tags;
noremap <leader>es :e! ~/.vim/UltiSnips/<cr>
" }}}

" session --------------------------------------------- {{{
let g:session_autosave = 'no'
" }}}

" miniyank --------------------------------------------- {{{
map p <Plug>(miniyank-autoput)
map P <Plug>(miniyank-autoPut)
map <C-p> <Plug>(miniyank-cycle)
" }}}

syntax on

" Colors and Fonts ---------------------- {{{
set t_Co=256
set background=dark
set termguicolors
set guifont=FiraCode\ Nerd\ Font\ 11

colorscheme base16-default-dark

if MySys() ==? "windows"
  set guifont=Lucida_Console:h10:cANSI
endif
" }}}

" vim-commentary ---------------------- {{{
autocmd FileType terraform setlocal commentstring=#%s
" }}}

" which key mapping ---------------------- {{{
nnoremap <silent> <leader>      :<c-u>WhichKey ','<CR>
nnoremap <silent> <localleader> :<c-u>WhichKey  '<Space>'<CR>
" }}}

" NERDTree ---------------------- {{{
map <leader>u :NERDTreeFind<cr>:wincmd p<cr>
map <leader>d :NERDTreeToggle<cr>
" }}}

" Terraform section -------------------------------------------- {{{
augroup terraform_group
  autocmd!
  autocmd FileType terraform inoremap <buffer> $f #--- PH ----------------------------------------------<esc>FP2xi
augroup END
" }}}

" Emacs Command Line ---------------------- {{{
" keep C-r for accessing registers
let g:EmacsCommandLineSearchCommandLineDisable = 1
" }}}

" airline ---------------------- {{{
let g:airline#extensions#tmuxline#enabled = 0
" }}}
